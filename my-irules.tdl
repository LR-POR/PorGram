;;; -*- Mode: TDL; Coding: utf-8 -*-
;;; Inflecting Lexical Rule Instances

;;; all vowels except o
%(letter-set (!v aeiuáéóãõôê))

%(letter-set (!s sx))
%(letter-set (!c nrz))
%(letter-set (!r nr))
%(letter-set (!o oa))
%(letter-set (!e aei))
%(letter-set (!i ei))
%(letter-set (!u ae))

;;; all Portuguese consonant letters
%(letter-set (!l bcdfgjlmnprstvxz))

;;; all Portuguese consonant letters except g
%(letter-set (!k bcdfjlmnprstvxz))

;;; g and q (e.g. "seguir" and "extorquir")
%(letter-set (!q gq))

;;; n, r and s (e.g. "mentir", "advertir" and "vestir", which undergo vowel alternation)
%(letter-set (!n nrs))

;;; all letters except c, g, ç and u
%(letter-set (!g abdefhijklmnopqrstvwxyzáéóõãôê))

;;; all letters except c, g and ç
%(letter-set (!y abdefhijklmnopqrstuvwxyzáéóõãôê))

;;; all letters except g
%(letter-set (!p abcdefhijklmnopqrstuvwxyzçáéóãõôê))

;;; all letters except ã
%(letter-set (!a abcdefghijklmnopqrstuvwxyzçáéóõôê))

;;; all letters except c, e, g, ç
%(letter-set (!x abdfhijklmnopqrstuvwxyzáéóõãôê))

;;; all letters except e
%(letter-set (!z abcdfghijklmnopqrstuvwxyzçáéóõãôê))

;;; all letters except l and m
%(letter-set (!m abcdefghijknopqrstuvwxyzçáéóõãôê))


;; Does this rule generate "hífens"? If it doesn't, this form should be included in the my-irregs.tab file,
;; if it is considered correct (it seems to be spurious, correct form is "hifens"), cf. issue #98 of MorphoBr
n-pl-suffix :=
%suffix (!s !s) (!ao !aos) (!v !vs) (ão ões) (!c !ces) (al ais) (ol óis) (el éis) (il is) (ês eses) (m ns) (ás ases) (ês eses) (és eses) (ís ises) (ós oses)
n-pl-lex-rule.

;;; Modify this rule according to the one above and vice-versa. It seems that they must be identical.
a-pl-suffix :=
%suffix (!s !s) (!ao !aos) (!v !vs) (ão ões) (r res) (z zes) (n nes) (al ais) (ol óis) (el éis) (il is) (ês eses) (m ns) (ás ases) (ês eses) (és eses) (ís íses) (ós oses) (rzinh!o rezinh!os) (ãozinho õezinhos) (ãozito õezitos) 
a-pl-lex-rule.

fem-suffix :=
%suffix (r ra) (!ao !aa) (eu eia) (ão ona) (ês esa) (éu oa) (!ru !rua)
fem-lex-rule.

det-fem-suffix :=
; (algum alguma) (este esta) (o a) (tal tal)
%suffix (um uma) (e a) (o a) (l l)
det-fem-lex-rule.

det-pl-suffix :=
; (algum alguns) (alguma algumas) (tal tais)
%suffix (m ns) (!m !ms) (l is)
det-pl-lex-rule.

dim-suffix1 :=
; (forte fortinho) (aceso acesinho) (feliz felizinho) (fraco fraquinho) (lindo lindinho) (fortes fortinhos) (acesos acesinhos) (felizes felizinhos) (fracos fraquinhos) (lindos lindinhos)
%suffix (e inho) (e inha) (s!o sinh!o) (z zinho) (z zinha) (c!o quinh!o) (!o inh!o) (es inhos) (es inhas) (s!os sinh!os) (zes zinhos) (zes zinhas) (c!os quinh!os) (!os inh!os)
dim-lex-rule.

dim-suffix2 :=
; (fraco fraquito) (forte fortito) (bom bonzito) (lindo lindito) (fracos fraquitos) (fortes fortitos) (bons bonzitos)
%suffix (c!o quit!o) (e ito) (e ita) (m nzito) (!o it!o) (c!os quit!os) (es itos) (es itas) (ns nzitos) (!os it!os)
dim-lex-rule.

dim-suffix3 :=
; (fortes fortezinhos) (lindos lindozinhos) (bons bonzinhos) (adoráveis adoraveizinhos) (forte fortezinho) (lindo lindozinho) (lambedor lambedorzinho) (adorável adoravelzinho) (bom bonzinho)
%suffix (!is !izinhos) (!os !ozin!os) (ns nzinhos) (ns nzinhas) (áveis aveizinhos) (áveis aveizinhas) (!i !izinho) (!i !izinha) (!o !ozinh!o) (r rzinho) (ável avelzinho) (ável avelzinha) (m nzinho) (m nzinha)
dim-lex-rule.

perf-ind-1sg-suffix :=
; (partir parti) (puir puí) (seguir segui) (vender vendi) (dar dei) (atacar ataquei) (caçar cacei) (chegar cheguei)
%suffix (!lir !li) (!kuir !kuí) (!quir !qui) (er i) (!yar !yei) (car quei) (çar cei) (gar guei)
perf-ind-1sg-lex-rule.

perf-ind-1pl-suffix :=
; (partir partimos) (puir puímos) (seguir seguimos) (vender vendemos) (doar doamos) (doar doámos)
%suffix  (!lir !limos) (!kuir !kuímos) (!quir !quimos) (!ur !umos) (ar ámos)
perf-ind-1pl-lex-rule.

perf-ind-2sg-suffix :=
; (partir partiste) (puir puíste) (seguir seguiste) (vender vendeste) (doar doaste)
%suffix (!lir !liste) (!kuir !kuíste) (!quir !quiste) (!ur !uste)
perf-ind-2sg-lex-rule.

perf-ind-2pl-suffix :=
; (partir partistes) (puir puístes) (seguir seguistes) (vender vendestes) (doar doastes)
%suffix (!lir !listes) (!kuir !kuístes) (!quir !quistes) (!ur !ustes)
perf-ind-2pl-lex-rule.

perf-ind-3sg-suffix :=
; (partir partiu) (vender vendeu) (doar doou)
%suffix (!ir !iu) (ar ou)
perf-ind-3sg-lex-rule.

perf-ind-3pl-suffix :=
; (partir partiram) (puir puíram) (seguir seguiram) (vender venderam) (doar doaram)
%suffix (!lir !liram) (!kuir !kuíram) (!quir !quiram) (!ur !uram)
perf-ind-3pl-lex-rule.

pres-ind-1sg-suffix :=
; (advertir advirto) (dormir durmo) (ferir firo) (agir ajo) (ressarcir ressarço) (partir parto) (puir puo) (distinguir distingo) (perseguir persigo)
; (proteger protejo) (vender vendo) (erguer ergo) (vencer venço) (atacar ataco) (passear passeio) 
%suffix (e!n!lir i!n!lo) (o!n!lir u!n!lo) (e!lir i!lo) (gir jo) (cir ço) (!gir !go) (!puir !puo) (!zguir !zgo) (seguir sigo) (ger jo) (!ger !go) (guer go) (cer ço) (!zar !zo) (ear eio)
pres-ind-1sg-lex-rule.

pres-ind-2sg-suffix :=
; (partir partes) (puir puis) (seguir segues) (vender vendes) (doar doas) (passear passeias)
%suffix (!lir !les) (!kuir !kuis) (!quir !ques) (er es) (!zar !zas) (ear eias)
pres-ind-2sg-lex-rule.

pres-ind-3sg-suffix :=
; (partir parte) (puir pui) (seguir segue) (vender vende) (doar doa) (passear passeia)
%suffix (!lir !le) (!kuir !kui) (!quir !que) (er e) (!zar !za) (ear eia)
pres-ind-3sg-lex-rule.

pres-ind-1pl-suffix :=
; (partir partimos) (puir puímos) (seguir seguimos) (vender vendemos) (doar doamos)
%suffix (!lir !limos) (!kuir !kuímos) (!quir !quimos) (!ur !umos)
pres-ind-1pl-lex-rule.

pres-ind-2pl-suffix :=
; (partir partis) (puir puís) (seguir seguis) (vender vendeis) (doar doais)
%suffix (!lir !lis) (!kuir !kuís) (!quir !quis) (!ur !uis)
pres-ind-2pl-lex-rule.

pres-ind-3pl-suffix :=
; (partir partem) (vender vendem) (doar doam) (passear passeiam)
%suffix (ir em) (er em) (!zar !zam) (ear eiam)
pres-ind-3pl-lex-rule.

pres-subj-1sg-suffix :=
; (advertir advirta) (dormir durma) (ferir fira) (agir aja) (ressarcir ressarça) (partir parta) (puir pua) (distinguir distinga) (perseguir persiga) (proteger proteja)
; (vender venda) (erguer erga) (vencer vença) (acuar acue) (atacar ataque) (caçar cace) (chegar chegue) (passear passeie) 
%suffix (e!n!lir i!n!la) (o!n!lir u!n!la) (e!lir i!la) (gir ja) (cir ça) (!gir !ga) (!puir !pua) (!zguir !zga) (seguir siga) (ger ja) (!ger !ga) (guer ga) (cer ça) (!xar !xe) (car que) (çar ce) (gar gue) (ear eie)
pres-subj-1sg-lex-rule.

pres-subj-3sg-suffix :=
;  (advertir advirta) (dormir durma) (ferir fira) (agir aja) (ressarcir ressarça) (partir parta) (puir pua) (distinguir distinga) (perseguir persiga) (proteger proteja)
; (vender venda) (erguer erga) (vencer vença) (acuar acue) (atacar ataque) (caçar cace) (chegar chegue) (passear passeie) 
%suffix (e!n!lir i!n!la) (o!n!lir u!n!la) (e!lir i!la) (gir ja) (cir ça) (!gir !ga) (!puir !pua) (!zguir !zga) (seguir siga) (ger ja) (!ger !ga) (guer ga) (cer ça) (!xar !xe) (car que) (çar ce) (gar gue) (ear eie)
pres-subj-3sg-lex-rule.

pres-subj-2sg-suffix :=
;  (advertir advirtas) (dormir durmas) (ferir firas) (agir ajas) (ressarcir ressarças) (partir partas) (puir puas) (distinguir distingas) (perseguir persigas) (proteger protejas)
; (vender vendas) (erguer ergas) (vencer venças) (acuar acues) (atacar ataques) (caçar caces) (chegar chegues) (passear passeies) 
%suffix  (e!n!lir i!n!las) (o!n!lir u!n!las) (e!lir i!las) (gir jas) (cir ças) (!gir !gas) (!puir !puas) (!zguir !zgas) (seguir sigas) (ger jas) (!ger !gas)  (guer gas) (cer ças) (!xar !xes) (car ques) (çar ces) (gar gues) (ear eies)
pres-subj-2sg-lex-rule.

pres-subj-1pl-suffix :=
; (advertir advirtamos) (dormir durmamos) (ferir firamos) (agir ajamos) (ressarcir ressarçamos) (partir partamos) (puir puamos) (distinguir distingamos) (perseguir persigamos) (proteger protejamos)
; (vender vendamos) (erguer ergamos) (vencer vençamos) (acuar acuemos) (caçar cacemos) (atacar ataquemos) (chegar cheguemos)
%suffix  (e!n!lir i!n!lamos) (o!n!lir u!n!lamos) (e!lir i!lamos) (gir jamos) (cir çamos) (!gir !gamos) (!puir !puamos) (!zguir !zgamos) (seguir sigamos) (ger jamos) (!ger !gamos) (guer gamos) (cer çamos) (!yar !yemos) (çar cemos) (car quemos) (gar guemos)
pres-subj-1pl-lex-rule.

pres-subj-2pl-suffix :=
; (advertir advirtais) (dormir durmais) (ferir firais) (agir ajais) (ressarcir ressarçais) (partir partais) (puir puais) (distinguir distingais) (perseguir persigais) (proteger protejais)
; (vender vendais) (erguer ergais) (vencer vençais) (acuar acueis) (caçar caceis) (atacar ataqueis) (chegar chegueis)
%suffix (e!n!lir i!n!lais) (o!n!lir u!n!lais) (e!lir i!lais) (gir jais) (cir çais) (!gir !gais) (!puir !puais) (!zguir !zgais) (seguir sigais) (ger jais) (!ger !gais) (guer gais) (cer çais) (!yar !yeis) (çar ceis) (car queis) (gar gueis)
pres-subj-2pl-lex-rule.

pres-subj-3pl-suffix :=
; (advertir advirtam) (dormir durmam) (ferir firam) (agir ajam) (ressarcir ressarçam) (partir partam) (puir puam) (distinguir distingam) (perseguir persigam) (proteger protejam)
; (vender vendam) (erguer ergam) (vencer vençam) (acuar acuem) (caçar cacem) (atacar ataquem) (chegar cheguem) (passear passeiem)
%suffix  (e!n!lir i!n!lam) (o!n!lir u!n!lam) (e!lir i!lam) (gir jam) (cir çam) (!gir !gam) (!puir !puam) (!zguir !zgam) (seguir sigam) (ger jam) (!ger !gam) (guer gam) (cer çam) (!xar !xem) (çar cem) (car quem) (gar guem) (ear eiem) 
pres-subj-3pl-lex-rule.

fut-ind-1sg-suffix :=
%suffix (r rei)
fut-ind-1sg-lex-rule.

fut-ind-1pl-suffix :=
%suffix (r remos)
fut-ind-1pl-lex-rule.

fut-ind-2sg-suffix :=
%suffix (r rás)
fut-ind-2sg-lex-rule.

fut-ind-3sg-suffix :=
%suffix (r rá)
fut-ind-3sg-lex-rule.

fut-ind-3pl-suffix :=
%suffix (r rão)
fut-ind-3pl-lex-rule.

fut-ind-2pl-suffix :=
%suffix (r reis)
fut-ind-2pl-lex-rule.

impf-ind-1sg-suffix :=
; (partir partia) (puir puía) (seguir seguia) (vender vendia) (doar doava)
%suffix (!lir !lia) (!kuir !kuía) (!quir !quia) (er ia) (ar ava)
impf-ind-1sg-lex-rule.

impf-ind-1pl-suffix :=
%suffix (ir íamos) (er íamos) (ar ávamos)
impf-ind-1pl-lex-rule.

impf-ind-3sg-suffix :=
; (partir partia) (puir puía) (seguir seguia) (vender vendia) (doar doava)
%suffix (!lir !lia) (!kuir !kuía) (!quir !quia) (er ia) (ar ava)
impf-ind-3sg-lex-rule.

impf-ind-2sg-suffix :=
; (partir partias) (puir puías) (seguir seguias) (vender vendias) (doar doavas)
%suffix(!lir !lias) (!kuir !kuías) (!quir !quias) (er ias) (ar avas)
impf-ind-2sg-lex-rule.

impf-ind-2pl-suffix :=
%suffix (ir íeis) (er íeis) (ar áveis)
impf-ind-2pl-lex-rule.

impf-ind-3pl-suffix :=
; (partir partiam) (puir puíam) (seguir seguiam) (vender vendiam) (doar doavam)
%suffix (!lir !liam) (!kuir !kuíam) (!quir !quiam) (er iam) (ar avam)
impf-ind-3pl-lex-rule.

fut-pret-1sg-suffix :=
%suffix (* ia)
fut-pret-1sg-lex-rule.

fut-pret-1pl-suffix :=
%suffix (* íamos)
fut-pret-1pl-lex-rule.

fut-pret-2sg-suffix :=
%suffix (* ias)
fut-pret-2sg-lex-rule.

fut-pret-2pl-suffix :=
%suffix (* íeis)
fut-pret-2pl-lex-rule.

fut-pret-3sg-suffix :=
%suffix (* ia)
fut-pret-3sg-lex-rule.

fut-pret-3pl-suffix :=
%suffix (* iam)
fut-pret-3pl-lex-rule.

pluperf-1sg-suffix :=
; (partir partira) (puir puíra) (seguir seguira) (vender vendera) (doar doara)
%suffix (!lir !lira) (!kuir !kuíra) (!quir !quira) (!ur !ura)
pluperf-1sg-lex-rule.

pluperf-1pl-suffix :=
%suffix (ir íramos) (er êramos) (ar áramos)
pluperf-1pl-lex-rule.

pluperf-2sg-suffix :=
; (partir partiras) (puir puíras) (seguir seguiras) (vender venderas) (doar doaras)
%suffix (!lir !liras) (!kuir !kuíras) (!quir !quiras) (!ur !uras)
pluperf-2sg-lex-rule.

pluperf-3sg-suffix :=
; (partir partira) (puir puíra) (seguir seguira) (vender vendera) (doar doara)
%suffix (!lir !lira) (!kuir !kuíra) (!quir !quira) (!ur !ura)
pluperf-3sg-lex-rule.

pluperf-3pl-suffix :=
; (partir partiram) (puir puíram) (seguir seguiram) (vender venderam) (doar doaram)
%suffix (!lir !liram) (!kuir !kuíram) (!quir !quiram) (!ur !uram)
pluperf-3pl-lex-rule.

pluperf-2pl-suffix :=
; (partir partíreis) (vender vendêreis) (comprar compráreis)
%suffix (ir íreis) (er êreis) (ar áreis)
pluperf-2pl-lex-rule.

impf-subj-1sg-suffix :=
; (partir partisse) (puir puísse) (seguir seguisse) (vender vendesse) (doar doasse)
%suffix (!lir !lisse) (!kuir !kuísse) (!quir !quisse) (!ur !usse)
impf-subj-1sg-lex-rule.

impf-subj-1pl-suffix :=
%suffix (ir íssemos) (er êssemos) (ar ássemos)
impf-subj-1pl-lex-rule.

impf-subj-3sg-suffix :=
; (partir partisse) (puir puísse) (seguir seguisse) (vender vendesse) (doar doasse)
%suffix (!lir !lisse) (!kuir !kuísse) (!quir !quisse) (!ur !usse)
impf-subj-3sg-lex-rule.

impf-subj-3pl-suffix :=
; (partir partissem) (puir puíssem) (seguir seguissem) (vender vendessem) (doar doassem)
%suffix (!lir !lissem) (!kuir !kuíssem) (!quir !quissem) (!ur !ussem)
impf-subj-3pl-lex-rule.

impf-subj-2sg-suffix :=
; (partir partisses) (puir puísses) (seguir seguisses) (vender vendesses) (doar doasses)
%suffix  (!lir !lisses) (!kuir !kuísses) (!quir !quisses) (!ur !usses)
impf-subj-2sg-lex-rule.

impf-subj-2pl-suffix :=
%suffix (ir ísseis) (er êsseis) (ar ásseis)
impf-subj-2pl-lex-rule.

fut-subj-1sg-suffix :=
%suffix (!er !er)
fut-subj-1sg-lex-rule.

fut-subj-1pl-suffix :=
%suffix (* mos)
fut-subj-1pl-lex-rule.

fut-subj-3sg-suffix :=
%suffix (!er !er)
fut-subj-3sg-lex-rule.

fut-subj-3pl-suffix :=
; (partir partirem) (puir puírem) (seguir seguirem) (vender venderem) (doar doarem)
%suffix (!lir !lirem) (!kuir !kuírem) (!quir !quirem) (!ur !urem)
fut-subj-3pl-lex-rule.

fut-subj-2sg-suffix :=
; (partir partires) (puir puíres) (seguir seguires) (vender venderes) (doar doares)
%suffix  (!lir !lires) (!kuir !kuíres) (!quir !quires) (!ur !ures)
fut-subj-2sg-lex-rule.

fut-subj-2pl-suffix :=
%suffix (* des)
fut-subj-2pl-lex-rule.

refl-mark-1s-suffix :=
; (queixo queixo-me)
%suffix (* -me)
refl-mark-1s-lex-rule.

refl-mark-1p-suffix :=
; (queixarmos quixarmo-nos)
%suffix (os o-nos)
refl-mark-1p-lex-rule.

refl-mark-3-suffix :=
%suffix (* -se)
refl-mark-3-lex-rule.

refl-mark-2s-suffix :=
%suffix (* -te)
refl-mark-2s-lex-rule.

refl-mark-2p-suffix :=
%suffix (* -vos)
refl-mark-2p-lex-rule.

pres-part-suffix :=
; (partir partindo) (vender vendendo) (doar doando)
%suffix (!er !endo)
pres-part-lex-rule.

past-part-suffix :=
; (partir partido) (puir puído) (seguir seguido) (vender vendido) (doar doado)
%suffix (!lir !lido) (!kuir !kuído) (!quir !quido) (er ido) (ar ado)
past-part-lex-rule.

infl-inf-3pl-suffix :=
; (partir partirem) (puir puírem) (seguir seguirem) (vender venderem) (doar doarem)
%suffix (!lir !lirem) (!kuir !kuírem) (!quir !quirem) (!ur !urem)
infl-inf-3pl-lex-rule.

infl-inf-2sg-suffix :=
; (partir partires) (puir puíres) (seguir seguires) (vender venderes) (doar doares)
%suffix  (!lir !lires) (!kuir !kuíres) (!quir !quires) (!ur !ures)
infl-inf-2sg-lex-rule.

infl-inf-2pl-suffix :=
; (partir partirdes) (vender venderdes) (doar doardes)
%suffix (!er !erdes)
infl-inf-2pl-lex-rule.

infl-inf-1pl-suffix :=
; (partir partirmos) (vender vendermos) (doar doarmos)
%suffix (!er !ermos)
infl-inf-1pl-lex-rule.

infl-inf-1sg-suffix :=
; (partir partir) (vender vender) (doar doar)
%suffix (!er !er)
infl-inf-1sg-lex-rule.

infl-inf-3sg-suffix :=
; (partir partir) (vender vender) (doar doar)
%suffix  (!er !er)
infl-inf-3sg-lex-rule.

cop-inf-suffix :=
%suffix (!ur !ur)
cop-inf-lex-rule.

cop-past-part-suffix :=
%suffix (ar ado) (er ido)
cop-past-part-lex-rule.

cop-pres-part-suffix :=
%suffix (!ur !undo)
cop-pres-part-lex-rule.

cop-pres-ind-1sg-suffix :=
%suffix (ar ou) (er ou)
cop-pres-ind-1sg-lex-rule.

cop-pres-ind-2sg-suffix :=
%suffix (ar ás) (ser és)
cop-pres-ind-2sg-lex-rule.

cop-pres-ind-3sg-suffix :=
%suffix (ar á) (ser é)
cop-pres-ind-3sg-lex-rule.

cop-pres-ind-1pl-suffix :=
%suffix (ar amos) (er omos)
cop-pres-ind-1pl-lex-rule.

cop-pres-ind-2pl-suffix :=
%suffix (ar ais) (er ois)
cop-pres-ind-2pl-lex-rule.

cop-pres-ind-3pl-suffix :=
%suffix (ar ão) (er ão)
cop-pres-ind-3pl-lex-rule.

cop-pres-subj-1sg-suffix :=
%suffix (ar eja) (er eja)
cop-pres-subj-1sg-lex-rule.

cop-pres-subj-2sg-suffix :=
%suffix (ar ejas) (er ejas)
cop-pres-subj-2sg-lex-rule.

cop-pres-subj-3sg-suffix :=
%suffix (ar eja) (er eja)
cop-pres-subj-3sg-lex-rule.

cop-pres-subj-1pl-suffix :=
%suffix (ar ejamos) (er ejamos)
cop-pres-subj-1pl-lex-rule.

cop-pres-subj-2pl-suffix :=
%suffix (ar ejais) (er ejais)
cop-pres-subj-2pl-lex-rule.

cop-pres-subj-3pl-suffix :=
%suffix (ar ejam) (er ejam)
cop-pres-subj-3pl-lex-rule.

cop-imperf-ind-1sg-suffix :=
%suffix (ar ava) (ser era)
cop-imperf-ind-1sg-lex-rule.

cop-imperf-ind-2sg-suffix :=
%suffix (ar avas) (ser eras)
cop-imperf-ind-2sg-lex-rule.

cop-imperf-ind-3sg-suffix :=
%suffix (ar ava) (ser era)
cop-imperf-ind-3sg-lex-rule.

cop-imperf-ind-1pl-suffix :=
%suffix (ar ávamos) (ser éramos)
cop-imperf-ind-1pl-lex-rule.

cop-imperf-ind-2pl-suffix :=
%suffix (ar aveis) (ser éreis)
cop-imperf-ind-2pl-lex-rule.

cop-imperf-ind-3pl-suffix :=
%suffix (ar avam) (ser eram)
cop-imperf-ind-3pl-lex-rule.

cop-perf-1sg-suffix :=
%suffix (ar ive) (ser fui)
cop-perf-1sg-lex-rule.

cop-perf-2sg-suffix :=
%suffix (ar iveste) (ser foste)
cop-perf-2sg-lex-rule.

cop-perf-3sg-suffix :=
%suffix (ar eve) (ser foi)
cop-perf-3sg-lex-rule.

cop-perf-1pl-suffix :=
%suffix (ar ivemos) (ser fomos)
cop-perf-1pl-lex-rule.

cop-perf-2pl-suffix :=
%suffix (ar ivestes) (ser fostes)
cop-perf-2pl-lex-rule.

cop-perf-3pl-suffix :=
%suffix (ar iveram) (ser foram)
cop-perf-3pl-lex-rule.

cop-fut-ind-1sg-suffix :=
%suffix (!ur !urei)
cop-fut-ind-1sg-lex-rule.

cop-fut-ind-2sg-suffix :=
%suffix  (!ur !urás)
cop-fut-ind-2sg-lex-rule.

cop-fut-ind-3sg-suffix :=
%suffix  (!ur !urá)
cop-fut-ind-3sg-lex-rule.

cop-fut-ind-1pl-suffix :=
%suffix (!ur !uremos)
cop-fut-ind-1pl-lex-rule.

cop-fut-ind-2pl-suffix :=
%suffix  (!ur !ureis)
cop-fut-ind-2pl-lex-rule.

cop-fut-ind-3pl-suffix :=
%suffix  (!ur !urão)
cop-fut-ind-3pl-lex-rule.

cop-fut-subj-1sg-suffix :=
%suffix (ar iver) (ser for)
cop-fut-subj-1sg-lex-rule.

cop-fut-subj-2sg-suffix :=
%suffix (ar iveres) (ser fores)
cop-fut-subj-2sg-lex-rule.

cop-fut-subj-3sg-suffix :=
%suffix (ar iver) (ser for)
cop-fut-subj-3sg-lex-rule.

cop-fut-subj-1pl-suffix :=
%suffix (ar ivermos) (ser formos)
cop-fut-subj-1pl-lex-rule.

cop-fut-subj-2pl-suffix :=
%suffix (ar iverdes) (ser fordes)
cop-fut-subj-2pl-lex-rule.

cop-fut-subj-3pl-suffix :=
%suffix (ar iverem) (ser forem)
cop-fut-subj-3pl-lex-rule.

cop-fut-pret-1sg-suffix :=
%suffix (!ur !uria)
cop-fut-pret-1sg-lex-rule.

cop-fut-pret-2sg-suffix :=
%suffix (!ur !urias)
cop-fut-pret-2sg-lex-rule.

cop-fut-pret-3sg-suffix :=
%suffix (!ur !uria)
cop-fut-pret-3sg-lex-rule.

cop-fut-pret-1pl-suffix :=
%suffix (!ur !uríamos)
cop-fut-pret-1pl-lex-rule.

cop-fut-pret-2pl-suffix :=
%suffix (!ur !uríeis)
cop-fut-pret-2pl-lex-rule.

cop-fut-pret-3pl-suffix :=
%suffix (!ur !uriam)
cop-fut-pret-3pl-lex-rule.

cop-imperf-subj-1sg-suffix :=
%suffix (ar ivesse) (ser fosse)
cop-imperf-subj-1sg-lex-rule.

cop-imperf-subj-2sg-suffix :=
%suffix (ar ivesses) (ser fosses)
cop-imperf-subj-2sg-lex-rule.

cop-imperf-subj-3sg-suffix :=
%suffix (ar ivesse) (ser fosse)
cop-imperf-subj-3sg-lex-rule.

cop-imperf-subj-1pl-suffix :=
%suffix (ar ivéssemos) (ser fôssemos)
cop-imperf-subj-1pl-lex-rule.

cop-imperf-subj-2pl-suffix :=
%suffix (ar ivésseis) (ser fôsseis)
cop-imperf-subj-2pl-lex-rule.

cop-imperf-subj-3pl-suffix :=
%suffix (ar ivessem) (ser fossem)
cop-imperf-subj-3pl-lex-rule.

cop-pqp-1sg-suffix :=
%suffix (ar ivera) (ser fora)
cop-pqp-1sg-lex-rule.

cop-pqp-2sg-suffix :=
%suffix (ar iveras) (ser foras)
cop-pqp-2sg-lex-rule.

cop-pqp-3sg-suffix :=
%suffix (ar ivera) (ser fora)
cop-pqp-3sg-lex-rule.

cop-pqp-1pl-suffix :=
%suffix (ar ivéramos) (ser fôramos)
cop-pqp-1pl-lex-rule.

cop-pqp-2pl-suffix :=
%suffix (ar ivéreis) (ser fôreis)
cop-pqp-2pl-lex-rule.

cop-pqp-3pl-suffix :=
%suffix (ar iveram) (ser foram)
cop-pqp-3pl-lex-rule.

cop-infl-inf-1sg-suffix :=
%suffix (!ur !ur)
cop-infl-inf-1sg-lex-rule.

cop-infl-inf-2sg-suffix :=
%suffix (!ur !ures)
cop-infl-inf-2sg-lex-rule.

cop-infl-inf-3sg-suffix :=
%suffix (!ur !ur)
cop-infl-inf-3sg-lex-rule.

cop-infl-inf-1pl-suffix :=
%suffix (!ur !urmos)
cop-infl-inf-1pl-lex-rule.

cop-infl-inf-2pl-suffix :=
%suffix (!ur !urdes)
cop-infl-inf-2pl-lex-rule.

cop-infl-inf-3pl-suffix :=
%suffix (!ur !urem)
cop-infl-inf-3pl-lex-rule.
